# ============================================================================
# Copyright (c) 2011-2012 University of Pennsylvania
# Copyright (c) 2013-2016 Andreas Schuh
# All rights reserved.
#
# See COPYING file for license information or visit
# https://cmake-basis.github.io/download.html#license
# ============================================================================

##############################################################################
# @file  generate_matlab_executable.cmake.in
# @brief Generate wrapper executable for invocation of MATLAB command.
#
# @note This file is generated by BASIS from the template file
#       generate_matlab_executable.cmake.in which is part of the BASIS installation.
#
# This script is configured using CMake's configure_sript() function instead
# of providing its arguments using the -D option of CMake before the -P option
# such that the build command can be made dependent on the configured build script.
##############################################################################

cmake_minimum_required (VERSION 2.8.12 FATAL_ERROR)

include ("@BASIS_MODULE_PATH@/MatlabTools.cmake") # basis_generate_matlab_executable()
                                                  # basis_write_addpaths_mfile()

# ----------------------------------------------------------------------------
# configuration
# ----------------------------------------------------------------------------

# general settings
set (MATLAB_EXECUTABLE    "@MATLAB_EXECUTABLE@")
set (MATLAB_COMMAND       "@MATLAB_COMMAND@")
# settings specific to build tree
set (OUTPUT_FILE          "@OUTPUT_FILE@")
set (BUILD_MATLABPATH     "@BUILD_MATLABPATH@")
set (BUILD_STARTUP_FILE   "@BUILD_STARTUP_FILE@")
# settings specific to installation
set (INSTALL_FILE         "@INSTALL_FILE@")
set (INSTALL_DIR          "@INSTALL_DIR@")
set (INSTALL_MATLABPATH   "@INSTALL_MATLABPATH@")
set (INSTALL_STARTUP_FILE "@INSTALL_STARTUP_FILE@")
set (INSTALL_STARTUP_DIR  "@INSTALL_STARTUP_DIR@")

# ----------------------------------------------------------------------------
# main
# ----------------------------------------------------------------------------

# write startup.m file for build tree
if (BUILD_STARTUP_FILE)
  file (WRITE "${BUILD_STARTUP_FILE}"
"% DO NOT edit. This file is generated by BASIS.
[mfiledir, ~, ~, ~] = fileparts(mfilename('fullpath'));\n")
  basis_write_addpaths_mfile ("${BUILD_STARTUP_FILE}" APPEND ${BUILD_MATLABPATH})
  set (BUILD_MATLABPATH)
endif ()
# generate executable for use in build tree
basis_generate_matlab_executable (
  "${OUTPUT_FILE}"
  COMMAND    "${MATLAB_COMMAND}"
  STARTUP    "${BUILD_STARTUP_FILE}"
  MATLABPATH ${BUILD_MATLABPATH}
)

if (INSTALL_FILE)
  # write startup.m file for build installation
  if (INSTALL_STARTUP_FILE)
    file (WRITE "${INSTALL_STARTUP_FILE}"
"% DO NOT edit. This file is generated by BASIS.
[mfiledir, ~, ~, ~] = fileparts(mfilename('fullpath'));\n")
    basis_write_addpaths_mfile ("${INSTALL_STARTUP_FILE}" APPEND ${INSTALL_MATLABPATH})
    set (INSTALL_MATLABPATH)
  endif ()
  # generate executable for installation
  get_filename_component (INSTALL_STARTUP_NAME "${INSTALL_STARTUP_FILE}" NAME)
  basis_generate_matlab_executable (
    "${INSTALL_FILE}"
    COMMAND     "${MATLAB_COMMAND}"
    STARTUP     "${INSTALL_STARTUP_DIR}/${INSTALL_STARTUP_NAME}"
    DESTINATION "${INSTALL_DIR}"
    MATLABPATH  ${INSTALL_MATLABPATH}
  )
endif ()
